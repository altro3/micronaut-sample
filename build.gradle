plugins {
    id 'io.micronaut.application' version '4.4.4'
    id 'io.micronaut.openapi' version '4.4.4'
    id 'org.openapi.generator' version '7.9.0'
}

ext {
    ver = [
            micronaut: '4.7.0',
            openapi  : '6.13.2-1!!',
    ]
}

mainClassName = "dev.itrust.sample.Application"
micronaut {
    version ver.micronaut
    runtime "netty"
    testRuntime "junit5"
    enableNativeImage false
    processing {
        group project.group
        incremental true
        annotations "com.micronaut.bug.*"
    }
    openapi {
        version = ver.openapi
        server(file("src/main/resources/openapi/openapi.yaml")) {
            apiPackageName = "com.example.openapi.api"
            modelPackageName = "com.example.openapi.api"
            useReactive = false
            useAuth = false
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

dependencies {

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "io.micronaut.validation:micronaut-validation"
    annotationProcessor "io.micronaut:micronaut-core-processor"
    annotationProcessor "io.micronaut.serde:micronaut-serde-processor"
    annotationProcessor "io.micronaut.security:micronaut-security-annotations"
    annotationProcessor "io.micronaut.openapi:micronaut-openapi:$ver.openapi"

    compileOnly "org.projectlombok:lombok"
    compileOnly "io.micronaut:micronaut-inject-java"
    compileOnly "io.micronaut.openapi:micronaut-openapi-annotations:$ver.openapi"
    compileOnly "com.fasterxml.jackson.core:jackson-annotations"

    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-jackson-core"
    implementation "io.micronaut.reactor:micronaut-reactor"
    implementation "io.micronaut.serde:micronaut-serde-api"
//    implementation "io.micronaut.security:micronaut-security-jwt"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "org.yaml:snakeyaml"
    runtimeOnly "io.micronaut.serde:micronaut-serde-jackson"
//    runtimeOnly "io.micronaut:micronaut-jackson-databind"

    testAnnotationProcessor "org.projectlombok:lombok"
    testAnnotationProcessor "io.micronaut:micronaut-core-processor"
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"

    testCompileOnly "org.projectlombok:lombok"
    testCompileOnly "io.micronaut:micronaut-inject-java"

    testImplementation "io.micronaut.test:micronaut-test-junit5"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs = [
            '-parameters',
            '-Xlint:unchecked',
            '-Xlint:deprecation'
    ]
    options.fork = true
//    options.forkOptions.jvmArgs += "-Dmicronaut.openapi.views.spec=swagger-ui.enabled=true,swagger-ui.js.url=/openapi/res/"
}

configurations.configureEach {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'minutes'
        cacheChangingModulesFor 0, 'minutes'
    }
}

compileJava {
    dependsOn("generateClientOpenApiApis", "generateClientOpenApiModels")
}

tasks.register("generateApi", org.openapitools.generator.gradle.plugin.tasks.GenerateTask.class) {
    generatorName.set("spring")
    library.set("spring-boot")
    inputSpec.set("$rootDir/src/main/resources/openapi/openapi.yaml")
    apiPackage.set("com.example.openapi.api")
    modelPackage.set("com.example.openapi.api")
    outputDir.set(layout.buildDirectory.dir("generated/openapi").get().toString())
//    enablePostProcessFile.set(true)
    skipOverwrite.set(false)
    generateModelTests.set(false)
    generateModelDocumentation.set(false)
    generateApiTests.set(false)
    generateApiDocumentation.set(false)
    skipValidateSpec.set(true)
    configOptions.set([
            "interfaceOnly" : "true",
            "skipDefaultInterface" : "true",
            "serializationLibrary" : "jackson",
            "openApiNullable" : "false",
            "serializableModel" : "true",
            "useSpringBoot3" : "true",
            "generateOneOfAnyOfWrappers" : "true",
            "useOneOfInterfaces" : "true",
            "useBeanValidation" : "true",
            "performBeanValidation" : "true",
    ])
    group = "openapi tools"
}
